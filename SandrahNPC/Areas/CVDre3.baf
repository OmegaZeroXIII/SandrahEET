IF
Global("SanShauMinsc","GLOBAL",29)
THEN
RESPONSE #100
SetGlobal("SanShauMinsc","GLOBAL",30)
StartCutSceneMode()
ActionOverride("CVBooTlk",DestroySelf())
ActionOverride("SanMyst",DestroySelf())
FadeToColor([20.0],0)
Wait(3)
RestorePartyLocations()
Wait(2)
FadeFromColor([20.0],0)
EndCutSceneMode()
RestParty()
END

IF
Global("SanHaerRel1","GLOBAL",13)
THEN
RESPONSE #100
ActionOverride("HaerDalis",SetGlobal("KickedOut","LOCALS",2))
ActionOverride("HaerDalis",ChangeAIScript("HaeHel1",OVERRIDE))
ActionOverride("HaerDalis",SetLeavePartyDialogFile())
ActionOverride("HaerDalis",LeaveParty())
ActionOverride("HaerDalis",ClearAllActions())
ActionOverride("HaerDalis",MoveBetweenAreas("CVHel1",[2794.2868],4))
CreateCreatureObjectOffset("CVHaer","CVSandr",[65.65])
SetGlobal("SanHaerRel1","GLOBAL",14)
END

IF
Global("SanHaerRel1","GLOBAL",14)
OR(2)
HPLT("CVHaer",40)
HPPercentLT("Alzalig",75)
THEN
RESPONSE #100
Kill("CVHaer")
SetGlobal("SanHaerRel1","GLOBAL",15)
END

IF
Global("SanHaerRel1","GLOBAL",15)
HPPercentLT("Narongu",50)
Global("Nargone","CVDre3",0)
THEN
RESPONSE #100
ActionOverride("Narongu",ReallyForceSpell(Myself,DRYAD_TELEPORT))
SetGlobal("Nargone","CVDre3",1)
END

IF
Global("SanHaerRel1","GLOBAL",15)
HPPercentLT("Alzalig",50)
Global("Alzgone","CVDre3",0)
THEN
RESPONSE #100
ActionOverride("Alzalig",ReallyForceSpell(Myself,DRYAD_TELEPORT))
SetGlobal("Alzgone","CVDre3",1)
END

IF
Global("Alzgone","CVDre3",1)
Global("Nargone","CVDre3",1)
Global("SanHaerRel1","GLOBAL",15)
!StateCheck(Player1,CD_STATE_NOTVALID)
CombatCounter(0)
!StateCheck("CVSandr",CD_STATE_NOTVALID)
Delay(12)
THEN
RESPONSE #100
CreateCreatureObjectOffset("sanmyst","CVSandr",[-200.-200])
SetGlobal("SanHaerRel1","GLOBAL",16)
END

IF
Global("SanHaerRel1","GLOBAL",17)
PartyHasItem("Sanhaer")
PartyHasItem("CVBloMa1")
Delay(12)
THEN
RESPONSE #100
RestorePartyLocations()
SetGlobal("SanHaerRel1","GLOBAL",18)
END

IF
Global("SanHealAmber","GLOBAL",13)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Player2,CD_STATE_NOTVALID)
!StateCheck(Player3,CD_STATE_NOTVALID)
!StateCheck(Player4,CD_STATE_NOTVALID)
!StateCheck(Player5,CD_STATE_NOTVALID)
!StateCheck(Player6,CD_STATE_NOTVALID)
Delay(12)
THEN
RESPONSE #100
RestorePartyLocations()
SetGlobal("SanHealAmber","GLOBAL",14)
END
