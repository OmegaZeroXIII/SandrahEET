IF
OnCreation()
Global("OpenArcive","ar0204",0)
GlobalLT("SanKhalBlood","GLOBAL",8)
THEN
RESPONSE #100
SetGlobal("OpenArcive","ar0204",1)
TriggerActivation("Archive",FALSE)
Continue()
END

IF
Global("CVAR0204","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("CVAR0204","GLOBAL",1)
Continue()
END

IF
GlobalGT("SanKhalBlood","GLOBAL",7)
OR(2)
Global("OpenArcive","ar0204",1)
Global("OpenArcive","ar0204",0)
THEN
RESPONSE #100
SetGlobal("OpenArcive","ar0204",2)
TriggerActivation("Archive",TRUE)
TriggerActivation("InfoBuilding",FALSE)
Continue()
END

IF
Global("SanKhalBlood","GLOBAL",10)
InParty("CVSandr")
Delay(16)
THEN
RESPONSE #100
SetGlobal("SanKhalBlood","GLOBAL",11)
END

IF
InParty("CVSandr")
Global("SK#NehtExists","AR0204",0)
Global("SanNoNeht","ar0204",0)
PartyHasItem("Misc5a")
!StateCheck(Player1,CD_STATE_NOTVALID)
CombatCounter(0)
!StateCheck("CVSandr",CD_STATE_NOTVALID)
Delay(16)
THEN
RESPONSE #100
SetGlobal("SanNoNeht","ar0204",1)
ActionOverride("CVSandr",MoveToObject(Player1))
ActionOverride("CVSandr",Interact("CVSandr"))
END

IF
Global("OpenArcive","ar0204",2)
Global("SanTalSeek","ar0204",0)
THEN
RESPONSE #100
SetGlobal("SanTalSeek","ar0204",1)
CreateCreature("CVTALOP1",[3225.2567],4)
END

IF
Global("SanTalSeek","ar0204",2)
THEN
RESPONSE #100
SetGlobal("SanTalSeek","ar0204",3)
CreateCreature("CVTALOW1",[3289.2739],5)
CreateCreature("CVTALOW1",[3303.2802],7)
CreateCreature("udumb02",[3041.2572],7)
END