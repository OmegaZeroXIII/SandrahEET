IF
InParty("Cernd")
AreaType(CITY)
AreaType(OUTDOOR)
!StateCheck("Cernd",CD_STATE_NOTVALID)
CombatCounter(0)
!StateCheck(Myself,CD_STATE_NOTVALID)
!See([ENEMY])
See("Cernd")
Global("SanCerndLT","GLOBAL",0)
Delay(12)
THEN
RESPONSE #100
SetGlobal("SanCerndLT","GLOBAL",1)
CreateCreatureObjectOffset("CVCatCe","CVSandr",[25.-25])
END

IF
InParty("Cernd")
Global("SanCerndLT","GLOBAL",3)
AreaType(FOREST)
PartyHasItem("CVCatCe")
CombatCounter(0)
!StateCheck(Myself,CD_STATE_NOTVALID)
!See([ENEMY])
See("Cernd")
Delay(12)
THEN
RESPONSE #100
SetGlobal("SanCerndLT","GLOBAL",4)
END

IF
InParty("Cernd")
Global("SanCerndLT","GLOBAL",6)
AreaType(OUTDOOR)
RealGlobalTimerExpired("SanCerndTI","LOCALS")
Delay(20)
THEN
RESPONSE #100
SetGlobal("SanCerndLT","GLOBAL",7)
END

IF
Global("SanCerndLT","GLOBAL",8)
Global("SanHealCernd","GLOBAL",6)
THEN
RESPONSE #100
SetGlobal("SanCerndLT","GLOBAL",9)
RealSetGlobalTimer("SanCerndTI","LOCALS",2800)
END

IF
RealGlobalTimerExpired("SanCerndTI","LOCALS")
InParty("Cernd")
OR(2)
Global("SanCerndLT","GLOBAL",9)
Global("SanCerndLT","GLOBAL",11)
!AreaType(OUTDOOR)
!AreaType(DUNGEON)
THEN
RESPONSE #100
IncrementGlobal("SanCerndLT","GLOBAL",1)
RealSetGlobalTimer("SanCerndTI","LOCALS",2800)
END

IF
GlobalGT("CerndIgarol","GLOBAL",1)
GlobalGT("SanCerndLT","GLOBAL",7)
Global("SanCerndBabe","LOCALS",0)
InParty("Cernd")
!PartyHasItem("misc8t")
THEN
RESPONSE #100
SetGlobal("SanCerndBabe","LOCALS",1)
RealSetGlobalTimer("SanCerndBTI","LOCALS",300)
END

IF
Global("SanCerndBabe","LOCALS",1)
RealGlobalTimerExpired("SanCerndBTI","LOCALS")
InParty("Cernd")
THEN
RESPONSE #100
SetGlobal("SanCerndBabe","LOCALS",2)
END

IF
OR(4)
Global("SanCerndLT","GLOBAL",4)
Global("SanCerndLT","GLOBAL",7)
Global("SanCerndLT","GLOBAL",10)
Global("SanCerndBabe","LOCALS",2)
CombatCounter(0)
!StateCheck("Cernd",CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
!See([ENEMY])
See("Cernd")
InParty("Cernd")
THEN
RESPONSE #100
RealSetGlobalTimer("SanCerndTI","LOCALS",1800)
MoveToObject("Cernd")
Interact("CVSandr")
END