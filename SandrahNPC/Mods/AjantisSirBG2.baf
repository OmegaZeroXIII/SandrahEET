IF
GlobalGT("SanHelmDogmas","GLOBAL",0)
InParty(Myself)
InParty("CVSandr")
Global("SanRomPath","GLOBAL",1)
Global("SanCAjBG2","LOCALS",0)
THEN
RESPONSE #100
SetGlobal("SanCAjBG2","LOCALS",1)
RealSetGlobalTimer("SanHelmDogmasdel","LOCALS",ONE_DAY)
END

IF
InParty(Myself)
InParty("CVSandr")
GlobalLT("SanHealSAjan","GLOBAL",5)
!Global("SanHealAjan","GLOBAL",6)
OR(7)
SpellCastOnMe("CVSandr",CLERIC_CURE_LIGHT_WOUNDS)
SpellCastOnMe("CVSandr",CLERIC_CURE_MEDIUM_WOUNDS)
SpellCastOnMe("CVSandr",CLERIC_CURE_CRITICAL_WOUNDS)
SpellCastOnMe("CVSandr",CLERIC_HEAL)
SpellCastOnMeRES("Spin109","CVSandr")
SpellCastOnMeRES("SanHand","CVSandr")
SpellCastOnMeRES("cvwoun","CVSandr")
THEN
RESPONSE #100
IncrementGlobal("SanHealSAjan","GLOBAL",1)
END

IF
AreaType(CITY)
InParty("CVSandr")
Global("SanAjaThief","GLOBAL",0)
OR(2)
Global("KnoAjFem","GLOBAL",3)
Global("KnoAjMale","GLOBAL",3)
CombatCounter(0)
Delay(18)
THEN
RESPONSE #100
SetGlobal("SanAjaThief","GLOBAL",1)
CreateCreatureObjectOffset("cvajam",Myself,[125.100])
CreateCreatureObjectOffset("cvajab",Myself,[165.120])
ActionOverride("cvajab",StartDialogueNoSet([PC]))
END

IF
Global("SanAjaThief","GLOBAL",2)
Global("SanHelmDogmas","GLOBAL",0)
Global("SanRomPath","GLOBAL",1)
THEN
RESPONSE #100
SetGlobal("SanHelmDogmas","GLOBAL",1)
RealSetGlobalTimer("SanHelmDogmasdel","LOCALS",300)
END

IF 
OR(3)
Global("SanHelmDogmas","GLOBAL",1)
Global("SanHelmDogmas","GLOBAL",5)
Global("SanHelmDogmas","GLOBAL",8)
InParty("CVSandr")
RealGlobalTimerExpired("SanHelmDogmasdel","LOCALS")
THEN
RESPONSE #100
IncrementGlobal("SanHelmDogmas","GLOBAL",1)
END

IF
GlobalGT("SanHelmDogmas","GLOBAL",6)
Global("SanAjantWDEntry","LOCALS",0)
OR(3)
AreaCheck("CVDre1")
AreaCheck("CVDre2")
AreaCheck("CVELM1")
THEN
RESPONSE #100
SetGlobal("SanAjantWDEntry","LOCALS",1)
END

IF
GlobalGT("SanHelmDogmas","GLOBAL",2)
GlobalLT("SanGavkilBant","GLOBAL",1)
Global("SanKilledAJ","GLOBAL",0)
RealGlobalTimerExpired("SanHelmDogmasdel","LOCALS")
AreaType(OUTDOOR) 
InParty("CVSandr")
!AreaCheck("CVDre1")
!AreaType(CITY)
Delay(10)
THEN
RESPONSE #100
SetGlobal("SanKilledAJ","GLOBAL",1)
CreateCreatureObjectOffset("sahobwiz",Myself,[100.100])
CreateCreatureObjectOffset("hobwiz01",Myself,[-100.-100])
CreateCreatureObjectOffset("hobwiz02",Myself,[100.-100])
CreateCreatureObjectOffset("saanwiz",Myself,[-100.100])
CreateCreatureObjectOffset("hobelite",Myself,[0.0])
END

IF
Global("ENDOFBG1","GLOBAL",2)
Dead("saanwiz")
Global("SanKilledAJ","GLOBAL",1)
InParty("CVSandr")
Global("SanAjkilBant","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("SanAjkilBant","GLOBAL",1)
END

IF
Global("SanHelmDogmas","GLOBAL",11)
InParty("CVSandr")
THEN
RESPONSE #100
SetGlobal("SanHelmDogmas","GLOBAL",12)
RealSetGlobalTimer("SanHelmDogmasdel","LOCALS",ONE_DAY)
END

IF
Global("SanHelmDogmas","GLOBAL",12)
InParty("CVSandr")
RealGlobalTimerExpired("SanHelmDogmasdel","LOCALS")
!AreaType(OUTDOOR)
!AreaType(DUNGEON)
THEN
RESPONSE #100
SetGlobal("SanHelmDogmas","GLOBAL",13)
END

IF
InParty(Myself)
InParty("CVSandr")
OR(9)
Global("SanHealSAjan","GLOBAL",5)
Global("SanHelmDogmas","GLOBAL",2)
Global("SanHelmDogmas","GLOBAL",6)
Global("SanHelmDogmas","GLOBAL",9)
Global("SanHelmDogmas","GLOBAL",13)
Global("SanHelmDogmas","GLOBAL",15)
Global("SanAjantWDEntry","LOCALS",1)
Global("SanAjkilBant","GLOBAL",1)
Global("Throwflowers","LOCALS",1)
!See([ENEMY])
CombatCounter(0)
!StateCheck("CVSandr",CD_STATE_NOTVALID)
THEN
RESPONSE #100
MoveToObject("CVSandr")
Interact(Player1)
END

IF
Global("SanHelmDogmas","GLOBAL",17)
Global("ENDOFBG1","GLOBAL",2)
InParty("CVSandr")
Global("SanRomPath","GLOBAL",1)
OR(17)
AreaCheck("ar0314")
AreaCheck("ar0406")
AreaCheck("ar0509")
AreaCheck("ar0511")
AreaCheck("ar0513")
AreaCheck("ar0704")
AreaCheck("ar0709")
AreaCheck("ar1105")
AreaCheck("ar1602")
AreaCheck("ar2010")
AreaCheck("ar2203")
AreaCheck("ar4267")
AreaCheck("dd3302")
AreaCheck("ra5104")
AreaCheck("BG3200")
AreaCheck("ys0396")
AreaCheck("rr3305")
THEN
RESPONSE #100
SetGlobal("SanHelmDogmas","GLOBAL",18)
END

IF
CombatCounter(0)
!See([ENEMY])
Global("SanHelmDogmas","GLOBAL",18)
THEN
RESPONSE #100
MoveToObject(Player1)
Interact(Player1)
END

IF
Global("C#AjantisPCMarriage","GLOBAL",1)
InParty("CVSandr")
Global("Throwflowers","LOCALS",0)
AreaType(OUTDOOR)
THEN
RESPONSE #100
CreateCreatureObjectOffset("CVWedGi",Myself,[-50.-50])
SetGlobal("Throwflowers","LOCALS",1)
END

