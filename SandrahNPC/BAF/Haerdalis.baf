IF
InParty("HaerDalis")
OR(2)
Global("SanHaerRel2","GLOBAL",1)
Global("SanHaerRel1","GLOBAL",1)
THEN
RESPONSE #100
SetGlobal("SanHaerRel1","GLOBAL",2)
SetGlobal("SanHaerRel2","GLOBAL",2)
RealSetGlobalTimer("SanHaerRelT","LOCALS",2000)
END

IF
InParty("HaerDalis")
Global("SanHaerRel2","GLOBAL",0)
Global("SanHaerRel1","GLOBAL",0)
Global("SanHaerRel3","LOCALS",0)
THEN
RESPONSE #100
SetGlobal("SanHaerRel3","LOCALS",1)
RealSetGlobalTimer("SanHaerRelT","LOCALS",200)
END

IF
Global("SanHaerRel3","LOCALS",1)
RealGlobalTimerExpired("SanHaerRelT","LOCALS")
THEN
RESPONSE #100
SetGlobal("SanHaerRel3","LOCALS",2)
END

IF
GlobalLT("SanHaerRel2","GLOBAL",3)
OR(2)
GlobalGT("SanHaerRel2","GLOBAL",0)
GlobalGT("SanHaerRel1","GLOBAL",0)
!AreaCheck("ar0705")
THEN
RESPONSE #100
SetGlobal("SanHaerRel2","GLOBAL",3)
END


IF
OR(2)
Global("SanHaerRel1","GLOBAL",2)
Global("SanHaerRel1","GLOBAL",4)
RealGlobalTimerExpired("SanHaerRelT","LOCALS")
AreaType(OUTDOOR)
!TimeOfDay(DAY)
InParty("HaerDalis")
THEN
RESPONSE #100
IncrementGlobal("SanHaerRel1","GLOBAL",1)
END

IF
InParty("Aerie")
InParty("HaerDalis")
GlobalLT("HaerDalisRomanceActive","GLOBAL",2)
Global("SanHaerRel1","GLOBAL",8)
PartyRested()
THEN
RESPONSE #100
IncrementGlobal("SanHaerRel1","GLOBAL",1)
END

IF
!InParty("Aerie")
InParty("HaerDalis")
Global("SanHaerRel1","GLOBAL",8)
RealGlobalTimerExpired("SanHaerRelT","LOCALS")
THEN
RESPONSE #100
IncrementGlobal("SanHaerRel1","GLOBAL",1)
END

IF
Global("SanHaerRel1","GLOBAL",10)
InParty("HaerDalis")
RealGlobalTimerExpired("SanHaerRelT","LOCALS")
!AreaType(CITY)
AreaType(OUTDOOR)
Delay(24)
THEN
RESPONSE #100
ClearAllActions()
StartCutSceneMode()
StorePartyLocations()
StartCutScene("CVCut217")
END

IF
InParty("HaerDalis")
!See([ENEMY])
See("HaerDalis")
!StateCheck("HaerDalis",CD_STATE_NOTVALID)
CombatCounter(0)
!StateCheck(Myself,CD_STATE_NOTVALID)
OR(7)
Global("SanHaerRel1","GLOBAL",3)
Global("SanHaerRel3","LOCALS",2)
Global("SanHaerRel1","GLOBAL",5)
Global("SanHaerRel1","GLOBAL",7)
Global("SanHaerRel1","GLOBAL",9)
Global("SanHaerRel1","GLOBAL",11)
Global("SanHaerRel1","GLOBAL",35)
THEN
RESPONSE #100
MoveToObject("HaerDalis")
Interact("CVSandr")
END

IF
Global("SanHaerRel1","GLOBAL",18)
PartyHasItemIdentified("CVBloMa1")
Delay(12)
THEN
RESPONSE #100
SetGlobal("SanHaerRel1","GLOBAL",19)
END

IF
Global("SanHaerRel1","GLOBAL",23)
OR(2)
AreaCheck("ar1404")
AreaCheck("ar1400")
Dead("DEATHKNS")
THEN
RESPONSE #100
TakeItemReplace("CVBloMa2","CVBloMa1","CVSandr")
SetGlobal("SanHaerRel1","GLOBAL",24)
END

IF
Global("SanHaerRel1","GLOBAL",25)
AreaCheck("ar1900")
Dead("DEATHKNR")
THEN
RESPONSE #100
TakeItemReplace("CVBloMa3","CVBloMa2","CVSandr")
SetGlobal("SanHaerRel1","GLOBAL",26)
END

IF
Global("SanHaerRel1","GLOBAL",26)
PartyHasItemIdentified("CVBloMa3")
Delay(12)
THEN
RESPONSE #100
SetGlobal("SanHaerRel1","GLOBAL",27)
END

IF
Global("SanHaerRel1","GLOBAL",28)
OR(6)
AreaCheck("ar0400")
AreaCheck("ar0500")
AreaCheck("ar0700")
AreaCheck("ar0800")
AreaCheck("ar0900")
AreaCheck("ar1000")
THEN
RESPONSE #100
CreateCreatureObjectOffset("brus2",Myself,[-50.-50])
SetGlobal("SanHaerRel1","GLOBAL",29)
END

IF
Global("SanHaerRel1","GLOBAL",34)
InParty("HaerDalis")
AreaType(OUTDOOR)
!AreaType(DUNGEON)
THEN
RESPONSE #100
SetGlobal("SanHaerRel1","GLOBAL",35)
END

IF
Global("SanHaerRel1","GLOBAL",36)
THEN
RESPONSE #100
AddSpecialAbility("HaePris")
AddSpecialAbility("CVSuccu")
SetGlobal("SanHaerRel1","GLOBAL",37)
END
