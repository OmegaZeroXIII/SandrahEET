IF
InParty("CVSandr")
!Dead("Sarevok")
Global("EndOfBG1","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("EndOfBG1","GLOBAL",-1)
END

IF
InParty("CVSandr")
Dead("Sarevok")
Global("EndOfBG1","GLOBAL",-1)
Global("SanEndBG1Plot","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("SanEndBG1Plot","GLOBAL",1)
END

IF
Global("SanEndBG1Plot","GLOBAL",2)
Global("EndOfBG1","GLOBAL",-1)
!InParty("CVJenlig")
THEN
RESPONSE #100
SetGlobal("EndOfBG1","GLOBAL",0)
END

IF
Global("SanEndBG1Plot","GLOBAL",2)
Global("EndOfBG1","GLOBAL",-1)
InParty("CVJenlig")
PartyHasItem("cvyensw2")
THEN
RESPONSE #100
SetGlobal("EndOfBG1","GLOBAL",0)
END

IF
Global("SarevokBehavior","GLOBAL",3)
InParty("CVSandr")
HPLT("Sarevok",35)
!Dead("galdor")
!InMyArea("galdor")
THEN
RESPONSE #100
CreateCreatureDoor("galdor",[965.482],0)
END

IF
Global("DMWWSarevokFight","GLOBAL",4)
THEN
RESPONSE #100
ActionOverride("Sarevok",DestroyItem("MONHP1"))
SetGlobal("DMWWSarevokFight","GLOBAL",5)
END

IF
Global("SanHaerRel1","GLOBAL",32)
Global("HaerResc","bg0125",0)
InParty("CVSandr")
THEN
RESPONSE #100
CreateCreature("alzalih",[722.787],15)
TriggerActivation("Door0123",FALSE)
SetGlobal("HaerResc","bg0125",1)
END

IF
Global("HaerResc","bg0125",1)
Dead("alzalih")
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Player2,CD_STATE_NOTVALID)
!StateCheck(Player3,CD_STATE_NOTVALID)
!StateCheck(Player4,CD_STATE_NOTVALID)
!StateCheck(Player5,CD_STATE_NOTVALID)
!StateCheck(Player6,CD_STATE_NOTVALID)
Delay(24)
THEN
RESPONSE #100
ClearAllActions()
StartCutSceneMode()
ActionOverride("CVSandr",MoveToPoint([503.470]))
MoveViewPoint([503.470],INSTANT)
SetGlobal("HaerResc","bg0125",2)
Wait(6)
ActionOverride("CVSandr",DisplayStringHead(Myself,@741))
Wait(8)
EndCutSceneMode()
Wait(1)
StartMovie("CVHELL1")
END

IF
Global("HaerResc","bg0125",2)
THEN
RESPONSE #100
ClearAllActions() 
StartCutSceneMode()
ActionOverride("CVSandr",DisplayStringHead(Myself,@742))
SetGlobal("HaerResc","bg0125",3)
TriggerActivation("Door0123",TRUE)
Wait(8)
StartCutScene("CVcut219")
END
