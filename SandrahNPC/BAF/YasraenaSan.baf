//Yasraena
IF
InParty("Yasraena")
Global("SanYasParty","LOCALS",0)
THEN
RESPONSE #100
SetGlobal("SanYasParty","LOCALS",1)
END

IF
InParty("Yasraena")
Global("SanYasfr","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("SanYasfr","GLOBAL",1)
RealSetGlobalTimer("SanYasInterv","LOCALS",1800)
END

IF
InParty("Yasraena")
!AreaType(DUNGEON)
RealGlobalTimerExpired("SanYasInterv","LOCALS")
OR(2)
Global("SanYasfr","GLOBAL",1)
Global("SanYasfr","GLOBAL",5)
THEN
RESPONSE #100
IncrementGlobal("SanYasfr","GLOBAL",1)
END

IF
InParty("Yasraena")
Global("SanYasfr","GLOBAL",3)
!InParty("P#Kivan")
OR(15)
AreaCheck("ar0501")
AreaCheck("ar0516")
AreaCheck("ar0711")
AreaCheck("ar0802")
AreaCheck("ar0906")
AreaCheck("ar0082")
AreaCheck("ar0201")
AreaCheck("ar0203")
AreaCheck("ar0404")
AreaCheck("ar0405")
AreaCheck("ar1201")
AreaCheck("ar1300")
AreaCheck("ar1400")
AreaCheck("ar1500")
AreaCheck("ar3001")
THEN
RESPONSE #100
SetGlobal("SanYasfr","GLOBAL",4)
END

IF
InParty("Yasraena")
Global("SanYasfr","GLOBAL",9)
AreaType(OUTDOOR)
!AreaType(CITY)
TimeOfDay(DAY)
THEN
RESPONSE #100
SetGlobal("SanYasfr","GLOBAL",10)
END

IF  
GlobalGT("SanYasfr","GLOBAL",8)
OR(4)
GlobalGT("JaheiraYasraenaTalk","GLOBAL",1)
GlobalGT("SanHintFactory","GLOBAL",26)
GlobalGT("C#AjantisTalkedToSurianko","GLOBAL",0)
GlobalGT("SanSaerFight","GLOBAL",15)
!AreaType(OUTDOOR)
!AreaType(DUNGEON)
Global("SanYasHus", "LOCALS",0)
THEN
RESPONSE #100
SetGlobal("SanYasHus", "LOCALS",1)
END

IF
Global("SanYasfr","GLOBAL",11)
THEN
RESPONSE #100
SetGlobal("SanYasfr","GLOBAL",12)
AddSpecialAbility("cvfreed")
END

IF
InParty("Yasraena")
Global("SanYasfr","GLOBAL",12)
!AreaType(DUNGEON)
RealGlobalTimerExpired("SanYasInterv","LOCALS")
PartyRested()
TimeLT(11)
TimeGT(4)
THEN
RESPONSE #100
SetGlobal("SanYasfr","GLOBAL",13)
END

IF
InParty("Yasraena")
Global("SanYasfr","GLOBAL",14)
AreaType(OUTDOOR)
!AreaType(CITY)
RealGlobalTimerExpired("SanYasInterv","LOCALS")
TimeOfDay(NIGHT)
GlobalGT("Chapter","GLOBAL",17)
THEN
RESPONSE #100
SetGlobal("SanYasfr","GLOBAL",15)
END

IF
InParty("Yasraena")
GlobalGT("SanYasfr","GLOBAL",8)
AreaCheck("ar2100")
!See([ENEMY])
CombatCounter(0)
Global("SanYasUD","LOCALS",0)
OR(3)
See("PortalAir")
See("PortalFire")
See("PortalEarth")
THEN
RESPONSE #100
SetGlobal("SanYasUD","LOCALS",1)
END

IF
InParty("Yasraena")
!AreaCheck("ar2102")
!See([ENEMY])
CombatCounter(0)
Global("SanYasUD","LOCALS",2)
Global("PlayerLooksLikeDrow","GLOBAL",1)
THEN
RESPONSE #100
SetGlobal("SanYasUD","LOCALS",3)
END

IF
Global("DivalirRescued","GLOBAL",1)
InParty("Yasraena")
!AreaCheck("ar2200")
!See([ENEMY])
CombatCounter(0)
Global("SanYasUD","LOCALS",4)
Delay(12)
THEN
RESPONSE #100
SetGlobal("SanYasUD","LOCALS",5)
END

IF
Global("Divalirrescued","GLOBAL",1)
AreaCheck("ar2800")
InParty("Yasraena")
Global("SanYasStay","LOCALS",0)
See("Divalir1")
THEN
RESPONSE #100
SetGlobal("SanYasStay","LOCALS",1)
END

IF
Global("SanYasStay","LOCALS",1)
InParty("Yasraena")
Global("CVAR2803","GLOBAL",1)
!See([ENEMY])
CombatCounter(0)
THEN
RESPONSE #100
SetGlobal("SanYasStay","LOCALS",2)
END

IF
Global("SanYasStay","LOCALS",3)
GlobalGT("YSROMANCECONFLICT","GLOBAL",0)
!Global("YasraenaRA","GLOBAL",1)
!Global("YasraenaRA","GLOBAL",2)
Global("YasraenaMatch","GLOBAL",1)
THEN
RESPONSE #100
SetGlobal("SanYasStay","LOCALS",4)
SetGlobal("MetDivalir","GLOBAL",2)
SetGlobal("YSROMANCECONFLICT","GLOBAL",0)
SetGlobal("YasraenaRA","GLOBAL",1)
ActionOverride("Yasraena",SetGlobal("YasraenaTalk","LOCALS",3))
END

IF
!See([ENEMY])
See("Yasraena")
!StateCheck("Yasraena",CD_STATE_NOTVALID)
CombatCounter(0)
!StateCheck(Myself,CD_STATE_NOTVALID)
OR(14)
Global("SanYasfr","GLOBAL",2)
Global("SanYasfr","GLOBAL",4)
Global("SanYasfr","GLOBAL",6)
Global("SanYasfr","GLOBAL",8)
Global("SanYasfr","GLOBAL",10)
Global("SanYasfr","GLOBAL",13)
Global("SanYasfr","GLOBAL",15)
Global("SanYasDams","LOCALS",1)
Global("SanYasHus","LOCALS",1)
Global("SanYasUD","LOCALS",1)
Global("SanYasUD","LOCALS",3)
Global("SanYasUD","LOCALS",5)
Global("YasraenaSvirfObjection","GLOBAL",3)
Global("SanYasStay","LOCALS",2)
THEN
RESPONSE #100
MoveToObject("Yasraena")
Interact("CVSandr")
END

IF
Global("SanYasHus","LOCALS",2)
THEN
RESPONSE #100
AddSpecialAbility("CVDra17")
SetGlobal("SanYasHus","LOCALS",3)
END