IF
Global("ENDOFBG1","GLOBAL",2)
InParty("Valygar")
Global("SanValgyR","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("SanValgyR","GLOBAL",1)
RealSetGlobalTimer("SanValgyTime","LOCALS",1000)
END

IF
Global("SanValgyR","GLOBAL",1)
RealGlobalTimerExpired("SanValgyTime","LOCALS")
InParty("Valygar")
OR(3)
AreaType(FOREST)
AreaCheck("ar1000")
AreaCheck("ar3597")
!TimeOfDay(NIGHT)
!See([ENEMY])
CombatCounter(0)
THEN
RESPONSE #100
SetGlobal("SanValgyR","GLOBAL",2)
RealSetGlobalTimer("SanValgyTime","LOCALS",1000)
END

IF
OR(3)
Global("SanValgyR","GLOBAL",5)
Global("SanValgyR","GLOBAL",7)
Global("SanValgyR","GLOBAL",9)
RealGlobalTimerExpired("SanValgyTime","LOCALS")
!AreaType(OUTDOOR)
!AreaType(DUNGEON)
InParty("Valygar")
Delay(12)
THEN
RESPONSE #100
IncrementGlobal("SanValgyR","GLOBAL",1)
RealSetGlobalTimer("SanValgyTime","LOCALS",1000)
END

IF
Global("TalkedLavok","GLOBAL",2)
GlobalGT("SanValgyR","GLOBAL",1)
AreaCheck("ar0411")
InParty("Valygar")
Global("SanValSph","LOCALS",0)
Delay(12)
THEN
RESPONSE #100
SetGlobal("SanValSph","LOCALS",1)
END

IF
GlobalGT("SanValgyR","GLOBAL",8)
InParty("Valygar")
InParty("Jaheira")
GlobalGT("SanJaheirBG2","GLOBAL",2)
Global("SanJahWarnVal","LOCALS",0)
AreaType(OUTDOOR)
AreaType(CITY)
THEN
RESPONSE #100
SetGlobal("SanJahWarnVal","LOCALS",1)
END

IF
OR(7)
Global("SanValgyR","GLOBAL",2)
Global("SanValgyR","GLOBAL",4)
Global("SanValgyR","GLOBAL",6)
Global("SanValSph","LOCALS",1)
Global("SanValgyR","GLOBAL",8)
Global("SanValgyR","GLOBAL",10)
Global("SanValgyR","GLOBAL",12)
InParty("Valygar")
!See([ENEMY])
See("Valygar")
!StateCheck("Valygar",CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
THEN
RESPONSE #100
MoveToObject("Valygar")
Interact("CVSandr")
END

IF
Global("SanValgyR","GLOBAL",13)
PartyRested()
THEN
RESPONSE #100
SetGlobal("SanValgyR","GLOBAL",14)
AddSpecialAbility("CVSphGol")
END