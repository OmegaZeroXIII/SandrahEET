IF
InParty(Myself)
InParty("CVSandr")
GlobalLT("SanHealSaer","GLOBAL",6)
OR(7)
SpellCastOnMe("CVSandr",CLERIC_CURE_LIGHT_WOUNDS)
SpellCastOnMe("CVSandr",CLERIC_CURE_MEDIUM_WOUNDS)
SpellCastOnMe("CVSandr",CLERIC_CURE_CRITICAL_WOUNDS)
SpellCastOnMe("CVSandr",CLERIC_HEAL)
SpellCastOnMeRES("SanHand","CVSandr")
SpellCastOnMeRES("Spin109","CVSandr")
SpellCastOnMeRES("cvwoun","CVSandr")
THEN
RESPONSE #100
IncrementGlobal("SanHealSaer","GLOBAL",1)
END

IF
InParty(Myself)
InParty("CVSandr")
Global("SanHealSaer","GLOBAL",3)
!See([ENEMY])
CombatCounter(0)
!StateCheck("CVSandr",CD_STATE_NOTVALID)
THEN
RESPONSE #100
MoveToObject("CVSandr")
Interact(Player1)
END

IF
Global("PlayerThiefGuild","GLOBAL",1)
InParty("CVSandr")
!AreaCheck("ar0900")
Global("SaeriLeaveThief","LOCALS",0)
Global("SansearAdvise","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("SansearAdvise","GLOBAL",1)
SetGlobal("SaeriLeaveThief","LOCALS",3)
END

IF
Gender(Player1,MALE)
Global("SanSaeCheck","LOCALS",0)
!Global("SaerilethRomanceActive","GLOBAL",1)
Alignment(Player1,MASK_GOOD)
InParty("CVSandr")
OR(3)
Race(Player1,HUMAN)
Race(Player1,HALF_ELF)
Race(Player1,ELF)
THEN
RESPONSE #100
SetGlobal("SanSaeCheck","LOCALS",1)
SetGlobal("CheckSaerilethMatch","GLOBAL",1)
SetGlobal("NSCharismaSafe","GLOBAL",2)
SetGlobal("SaerilethMatch","GLOBAL",1)
SetGlobal("SaerilethRomanceActive","GLOBAL",1)
SetGlobal("SaerilethLoveTalk","LOCALS",1)
RealSetGlobalTimer("SaerilethLoveTalkTime","GLOBAL",300)
END

IF
Global("SaerilethLoveTalk","LOCALS",17)
InParty("CVSandr")
Global("SanseaYou","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("SanseaYou","GLOBAL",1)
END

IF
GlobalGT("NSImmyGrillSaeriB","LOCALS",2)
Global("SanseaYou","GLOBAL",4)
InParty("Imoen2")
InParty("CVSandr")
THEN
RESPONSE #100
SetGlobal("SanseaYou","GLOBAL",5)
END

IF
GlobalGT("SaerilethLoveTalk","LOCALS",25)
InParty("CVSandr")
Global("SanSaerFight","GLOBAL",0)
OR(2)
Global("SaerilethRomanceActive","GLOBAL",1)
Global("SaerilethRomanceActive","GLOBAL",2)
THEN
RESPONSE #100
SetGlobal("SanSaerFight","GLOBAL",1)
END

IF
GlobalGT("SaerilethLoveTalk","LOCALS",40)
Global("SanSaerFight","GLOBAL",9)
Global("SaerilethRomanceActive","GLOBAL",2)
InParty("CVSandr")
THEN
RESPONSE #100
SetGlobal("SanSaerFight","GLOBAL",10)
END


IF
Global("SanSaerFight","GLOBAL",4)
HPGT(Myself,49)
THEN
RESPONSE #100
AttackOneRound("CVSandr")
END

IF
!Allegiance(Myself,PC)
Global("SanSaerFight","GLOBAL",5)
THEN
RESPONSE #100
ChangeEnemyAlly(Myself,PC)
END

