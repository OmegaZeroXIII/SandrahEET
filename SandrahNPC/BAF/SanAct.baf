IF
	ActionListEmpty()
	Class(LastSeenBy(Myself),HOBGOBLIN)
	InWeaponRange(LastSeenBy(Myself))
THEN
	RESPONSE #100
		AttackReevaluate(LastSeenBy(Myself),30)
END

IF
	AreaCheck("ar0602")
	See(Player1)
	CombatCounter(0)
	OR(2)
		Global("SandrahJoined","GLOBAL",52)
		Global("SandrahJoined","GLOBAL",6)
	Global("SanstartAI","ar0602",1)
THEN
	RESPONSE #100
		SetGlobal("SanstartAI","ar0602",2)
		MoveToObject(Player1)
		Dialogue(Player1)
END

IF
	BreakingPoint()
	StateCheck(Myself,STATE_PANIC)
THEN
	RESPONSE #100
		Calm(Myself)
		Continue()
END


IF
	ActionListEmpty()
	See(NearestEnemyOf(Myself))
	HaveSpell(CLERIC_SLOW_POISON)
	See([PC])
	StateCheck(LastSeenBy(Myself),STATE_POISONED)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_SLOW_POISON)
END

IF
	ActionListEmpty()
	See(NearestEnemyOf(Myself))
	HaveSpell(CLERIC_NEUTRALIZE_POISON)
	See([PC])
	StateCheck(LastSeenBy(Myself),STATE_POISONED)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_NEUTRALIZE_POISON)
END

IF
	ActionListEmpty()
	See(NearestEnemyOf(Myself))
	HaveSpell(CLERIC_MASS_CURE)
	See([PC])
	OR(2)
		HPPercentLT(Player1,60)
		HPPercentLT(Player3,60)
	OR(2)
		HPPercentLT(Player2,60)
		HPPercentLT(Player5,60)
	OR(2)
		HPPercentLT(Player4,60)
		HPPercentLT(Player6,60)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_MASS_CURE)
END

IF
	ActionListEmpty()
	HPPercentLT(Myself,30)
	HaveSpell(CLERIC_HEAL)
THEN
	RESPONSE #100
		Spell(Myself,CLERIC_HEAL)
END

IF
	ActionListEmpty()
	HPPercentLT(Myself,60)
	HaveSpell(CLERIC_CURE_SERIOUS_WOUNDS)
THEN
	RESPONSE #100
		Spell(Myself,CLERIC_CURE_SERIOUS_WOUNDS)
END

IF
	See(NearestEnemyOf(Myself))
	!Range(LastSeenBy(Myself),10)
	HPPercentLT(Myself,75)
	HaveSpell(CLERIC_CURE_LIGHT_WOUNDS)
THEN
	RESPONSE #100
		Spell(Myself,CLERIC_CURE_LIGHT_WOUNDS)
END

IF
	See(NearestEnemyOf(Myself))
	!Range(LastSeenBy(Myself),10)
	HPPercentLT("Haiass",60)
	HaveSpell(CLERIC_CURE_LIGHT_WOUNDS)
THEN
	RESPONSE #100
		Spell("Haiass",CLERIC_CURE_LIGHT_WOUNDS)
END

IF
	See([ENEMY.0.VAMPIRE])
	ActuallyInCombat()
	HaveSpell(CLERIC_NEGATIVE_PLANE_PROTECTION)
	See([PC])
	CheckStat(LastSeenBy(Myself),0,SCRIPTINGSTATE3)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_NEGATIVE_PLANE_PROTECTION)
END

IF
	ActionListEmpty()
	See(NearestEnemyOf(Myself))
	Range(NearestEnemyOf(Myself),20)
	!ModalState(TURNUNDEAD)
THEN
	RESPONSE #100
	EquipMostDamagingMelee()
	AttackReevaluate(NearestEnemyOf(Myself),30)
END

IF
	AttackedBy([ANYONE],DEFAULT)
	ActionListEmpty()
THEN
	RESPONSE #100
	AttackOneRound(LastAttackerOf(Myself))
END
