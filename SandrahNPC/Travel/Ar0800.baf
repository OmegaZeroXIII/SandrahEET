IF
Global("CVAR0800","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("CVAR0800","GLOBAL",1)
Continue()
END

IF
Global("JosterSilence","GLOBAL",1)
InParty("CVSandr")
GlobalGT("BodhiJob","GLOBAL",0)
Global("PlayerThiefGuild","GLOBAL",1)
Global("TSBodhiThief","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("TSBodhithief","GLOBAL",2)
END

IF
PartyHasItem("Misc5N")
Global("SanTirdir","ar0800",0)
InParty("CVSandr")
THEN
RESPONSE #100
SetGlobal("SanTirdir","ar0800",1)
ActionOverride("CVSandr",SetGlobal("SanTirdir","LOCALS",1))
END

IF
RealGlobalTimerExpired("CVColleAgain","GLOBAL")
InParty("CVSandr")
OR(3)
Global("CVColleEnc","GLOBAL",1)
Global("CVColleEnc","GLOBAL",4)
Global("CVColleEnc","GLOBAL",7)
Delay(12)
THEN
RESPONSE #100
IncrementGlobal("CVColleEnc","GLOBAL",1)
CreateCreatureObjectOffset("CVColle",Player1,[-45.35])
END

IF
Global("UcmgMatronHostile","GLOBAL",1)
Global("UcmGabDrow","GLOBAL",0)
THEN
RESPONSE #100
CreateCreature("UCMGDP01",[1575.500],10)
CreateCreature("UCMGMA03",[1525.530],9)
CreateCreature("UCMGMM01",[1272.671],6)
CreateCreature("UCMGWA01",[1500.575],6)
CreateCreature("UCMGWA02",[1466.600],5)
CreateCreature("UCMGDW01",[1400.633],6)
CreateCreature("UCMGDW02",[1421.670],0)
SetGlobal("UcmGabDrow","GLOBAL",1)
END

IF
GlobalGT("Chapter","GLOBAL",19)
Global("SanBodAttmpt","AR0800",0)
InMyArea("CVSandr")
THEN
RESPONSE #100
ClearAllActions()
StartCutSceneMode()
SetGlobal("SanBodAttmpt","AR0800",1)
FadeToColor([20.0],0)
StorePartyLocations()
SmallWait(2)
ActionOverride(Player1,LeaveAreaLUA("CVGrav","",[528.619],10))
ActionOverride(Player2,LeaveAreaLUA("CVGrav","",[528.619],10))
ActionOverride(Player3,LeaveAreaLUA("CVGrav","",[528.619],10))
ActionOverride(Player4,LeaveAreaLUA("CVGrav","",[528.619],10))
ActionOverride(Player5,LeaveAreaLUA("CVGrav","",[528.619],10))
ActionOverride(Player6,LeaveAreaLUA("CVGrav","",[528.619],10))
FadeFromColor([30.0],0)
StartCutScene("SanCut53")
END

IF
Global("SanBodAttmpt","AR0800",1)
InMyArea("CVSandr")
OR(21)
GlobalGT("KiaraVampire","GLOBAL",0)
GlobalGT("K#SarahVampire","GLOBAL",0)
GlobalGT("ValygarVampire","GLOBAL",0)
GlobalGT("VP_SimeVampire","GLOBAL",0)
GlobalGT("JaheiraVampire","GLOBAL",0)
GlobalGT("AerieVampire","GLOBAL",0)
GlobalGT("AnomenVampire","GLOBAL",0)
GlobalGT("ViconiaVampire","GLOBAL",0)
GlobalGT("M#AmberVampire","GLOBAL",0)
GlobalGT("C#AjantisVampire","GLOBAL",0)
GlobalGT("KimVampire","GLOBAL",0)
GlobalGT("ADAngelVampire","GLOBAL",0)
GlobalGT("LK#NindeAbducted","GLOBAL",0)
GlobalGT("G#TF.TyrisVampire","GLOBAL",0)
GlobalGT("B!GavVampire","GLOBAL",0)
GlobalGT("PGNaliaVampire","GLOBAL",0)
GlobalGT("EdwinVampire","GLOBAL",0)
GlobalGT("J#KelseyVampire","GLOBAL",0)
GlobalGT("FHFVampire","GLOBAL",0)
GlobalGT("Solavampire","GLOBAL",0)
GlobalGT("LE#TashiaVampire","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("SanBodAttmpt","AR0800",2)
END

IF
Global("SanBodAttmpt","AR0800",3)
InMyArea("CVSandr")
Dead("C6Bodhi")
THEN
RESPONSE #100
SetGlobal("SanBodAttmpt","AR0800",4)
END

IF
Global("LassalVampires","GLOBAL",3)
InParty("CVSandr")
Global("BoIrSibl","ar0800",0)
!StateCheck(Player1,CD_STATE_NOTVALID)
CombatCounter(0)
!StateCheck("CVSandr",CD_STATE_NOTVALID)
THEN
RESPONSE #100
SetGlobal("BoIrSibl","ar0800",1)
ActionOverride("CVSandr",MoveToObject(Player1))
ActionOverride("CVSandr",StartDialogNoSet("CVSandr"))
END

