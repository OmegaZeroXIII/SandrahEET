IF
OR(7)
Global("SansearAdvise","GLOBAL",1)
Global("SansearAdvise","GLOBAL",3)
Global("SanHealSaer","GLOBAL",6)
Global("SanUddSar","LOCALS",1)
Global("SanSaerFight","GLOBAL",5)
Global("SanSaerFight","GLOBAL",12)
Global("SanSaerFight","GLOBAL",15)
!See([ENEMY])
See("Saerileth")
!StateCheck("Saerileth",CD_STATE_NOTVALID)
CombatCounter(0)
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
MoveToObject("Saerileth")
Interact("CVSandr")
END

IF
OR(2)
Global("SanseaYou","GLOBAL",1)
Global("SanseaYou","GLOBAL",5)
THEN
RESPONSE #100
IncrementGlobal("SanseaYou","GLOBAL",1)
RealSetGlobalTimer("SanSaerT","LOCALS",300)
END

IF
Global("SanseaYou","GLOBAL",2)
InParty("Saerileth")
RealGlobalTimerExpired("SanSaerT","LOCALS")
THEN
RESPONSE #100
SetGlobal("SanseaYou","GLOBAL",3)
RealSetGlobalTimer("SanSaerT","LOCALS",800)
END

IF
Global("SanseaYou","GLOBAL",6)
InParty("Saerileth")
InParty("Imoen2")
RealGlobalTimerExpired("SanSaerT","LOCALS")
THEN
RESPONSE #100
SetGlobal("SanseaYou","GLOBAL",7)
RealSetGlobalTimer("SanSaerT","LOCALS",800)
END

IF
Global("NSInvisChallenge","GLOBAL",4)
Dead("uddeath2")
NumDeadGT("uddeath",1)
Global("SanUddSar","LOCALS",0)
Delay(6)
THEN
RESPONSE #100
SetGlobal("SanUddSar","LOCALS",1)
END

IF
Global("SanSaerFight","GLOBAL",1)
Global("SaerilethRomanceActive","GLOBAL",2)
InParty("Saerileth")
THEN
RESPONSE #100
SetGlobal("SanSaerFight","GLOBAL",2)
RealSetGlobalTimer("SanSaerT","LOCALS",800)
END

IF
OR(2)
Global("SanSaerFight","GLOBAL",2)
Global("SanSaerFight","GLOBAL",11)
RealGlobalTimerExpired("SanSaerT","LOCALS")
!AreaType(DUNGEON)
!AreaType(CITY)
AreaType(OUTDOOR)
!See([ENEMY])
See("Saerileth")
!StateCheck("Saerileth",CD_STATE_NOTVALID)
CombatCounter(0)
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
IncrementGlobal("SanSaerFight","GLOBAL",1)
END

IF
Global("SanSaerFight","GLOBAL",4)
HPPercentLT("Saerileth",50)
THEN
RESPONSE #100
ClearAllActions()
SetGlobal("SanSaerFight","GLOBAL",5)
Wait(2)
ActionOverride("Saerileth",ChangeEnemyAlly("Saerileth",PC))
Wait(2)
ActionOverride("CVSandr",ChangeEnemyAlly("CVSandr",PC))
ActionOverride("CVSandr",ReallyForceSpell("Saerileth",CLERIC_CURE_SERIOUS_WOUNDS))
END

IF
Global("SanSaerFight","GLOBAL",6)
!AreaType(OUTDOOR)
!AreaType(DUNGEON)
RealGlobalTimerExpired("SanSaerT","LOCALS")
See("Saerileth")
!StateCheck("Saerileth",CD_STATE_NOTVALID)
CombatCounter(0)
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
SetGlobal("SanSaerFight","GLOBAL",7)
END

IF
Global("SanSaerFight","GLOBAL",10)
THEN
RESPONSE #100
SetGlobal("SanSaerFight","GLOBAL",11)
RealSetGlobalTimer("SanSaerT","LOCALS",600)
END

IF
Global("DefeatedJon","GLOBAL",2)
InParty("Saerileth")
PartyHasItem("NSRNG")
Global("SanSaerFight","GLOBAL",13)
THEN
RESPONSE #100
SetGlobal("SanSaerFight","GLOBAL",14)
RealSetGlobalTimer("SanSaerT","LOCALS",200)
END

IF
Global("SanSaerFight","GLOBAL",14)
RealGlobalTimerExpired("SanSaerT","LOCALS")
THEN
RESPONSE #100
SetGlobal("SanSaerFight","GLOBAL",15)
END